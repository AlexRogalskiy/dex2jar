plugins {
    id "application"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

allprojects {
    group = 'com.googlecode.d2j'
    version = '2.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = targetCompatibility = 1.8

    dependencies {
        implementation fileTree(dir: 'libs', include: '*.jar')
        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    }

    [compileJava, compileTestJava]*.options.collect { options -> options.encoding = 'UTF-8' }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

mainClassName = "com.googlecode.dex2jar.tools.Dex2jarCmd"
archivesBaseName = "dex2jar"

dependencies {
    implementation(project(":d2j-base-cmd")) {
        transitive = false
    }
    implementation(project(":d2j-jasmin")) {
        transitive = false
    }
    implementation(project(":d2j-smali")) {
        transitive = false
    }
    implementation(project(":dex-ir")) {
        transitive = false
    }
    implementation(project(":dex-reader")) {
        transitive = false
    }
    implementation(project(":dex-reader-api")) {
        transitive = false
    }
    implementation(project(":dex-tools")) {
        transitive = false
    }
    implementation(project(":dex-translator")) {
        transitive = false
    }
    implementation(project(":dex-writer")) {
        transitive = false
    }
    implementation fileTree(dir: 'libs', include: '*.jar')
}

tasks.jar.enabled = false // Don't build normal jar

shadowJar {
    archiveClassifier.set(null)
}

// Get rid of Gradle warnings
tasks.distTar.dependsOn tasks.shadowJar
tasks.distZip.dependsOn tasks.shadowJar
tasks.startScripts.dependsOn tasks.shadowJar
